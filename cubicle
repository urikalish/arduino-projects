#include <SPI.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define TRIG_PIN  4
#define ECHO_PIN  5
#define LED_PIN   7
#define RELAY_PIN 8

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET     4

#define DISTANCE_THRESHOLD_CM 50
#define FAR_DELAY_MILLIS 10000

String line1 = "Cubicle Radar  v1.0.0";
String line2 = "--- by Uri Kalish ---";

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);
boolean detectTarget;
boolean isFar;
boolean isRelayOn;
unsigned long lastDetectTime;
unsigned long nearStartTime;
unsigned long farStartTime;

void setup() {
  Serial.begin(9600);
  pinMode(ECHO_PIN, INPUT);
  pinMode(TRIG_PIN, OUTPUT);
  digitalWrite(TRIG_PIN, LOW);
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, LOW);
  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, LOW);
  pinMode(RELAY_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, HIGH);
  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    Serial.println(F("SSD1306 allocation failed"));
    for(;;);
  }
  display.display();
  display.clearDisplay();
  lastDetectTime = millis();  
  nearStartTime = millis();
  farStartTime = millis();
  detectTarget = true;
  isRelayOn = true;
  isFar = false;
}

void loop() {
  int distance = radarPing();   
  if (distance <= DISTANCE_THRESHOLD_CM) {
    detectTarget = true;
    digitalWrite(LED_BUILTIN, HIGH);
    digitalWrite(LED_PIN, HIGH);
    lastDetectTime = millis();
    if (isFar) {
      isFar = false;
      nearStartTime = millis();
    }
  } else {
    detectTarget = false;
    digitalWrite(LED_BUILTIN, LOW);
    digitalWrite(LED_PIN, LOW);
    if (!isFar && ((millis() - lastDetectTime) > FAR_DELAY_MILLIS)) {
      isFar = true;
      farStartTime = millis();
    }
  }
  if (isFar) {
    isRelayOn = false;
    digitalWrite(RELAY_PIN, LOW);
  } else {
    isRelayOn = true;
    digitalWrite(RELAY_PIN, HIGH);
  }
  goDisplay(distance);
  delay(100);
}

int radarPing() {
  long duration;
  int distance;
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  duration = pulseIn(ECHO_PIN, HIGH);
  distance = duration*0.0343/2;
  //Serial.println(distance);   
  return distance; 
}

void goDisplay(int distance) {
  display.clearDisplay();
  display.setTextColor(WHITE);        
  display.setCursor(0,0);             
  display.setTextSize(1);
  
  display.println(line1);

  display.println(line2);

  unsigned long curTime = millis();
  
  display.print("Run Time ");
  //unsigned long totalTime = curTime / 1000.0;
  unsigned long totalTime = curTime;
  String totalTimeStr = String(totalTime);
  display.println(totalTimeStr);

  display.print("Rng/Trg  ");
  String rngTrg = "";
  if (distance <= 2000) {
    String distanceStr = String(distance);
    rngTrg += distanceStr;
  } else {
    rngTrg += "0";
  }
  if (detectTarget) {
    rngTrg += "/YES";  
  } else {
    rngTrg += "/NO";  
  }
  display.println(rngTrg);
  
  display.print("Near/Far ");
  if (!isFar) {
    unsigned long nearTime = (curTime - nearStartTime) / 1000.0;
    String nearTimeStr = String(nearTime);
    display.print(nearTimeStr);
    display.println("/0");
  } else {  
    display.print("0/");
    unsigned long farTime = (curTime - farStartTime) / 1000.0;
    String farTimeStr = String(farTime);
    display.println(farTimeStr);
  }
   
  display.print("Relay    ");
  if (isRelayOn) {
    display.println("ON");  
  } else {
    display.println("OFF");
  }

  display.println("---------------------");

  display.println("---------------------");
   
  display.display();
}
